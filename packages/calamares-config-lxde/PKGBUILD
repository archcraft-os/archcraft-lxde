# Maintainer: Aditya Shakya <adi1090x@gmail.com>

pkgname=calamares-config-lxde
pkgver=1.0
pkgrel=6
pkgdesc="Calamares configuration for Archcraft LXDE."
arch=('any')
url="https://github.com/archcraft-os/archcraft-lxde"
license=('GPL')
conflicts=('calamares-config' 'calamares-config-xfce' 'calamares-config-prime' 'calamares-config-wayland')

prepare() {
	cp -af ../files/. "$srcdir"
}

package() {
	local _bindir="$pkgdir"/usr/bin
	mkdir -p "$_bindir"

	# copy all files recursively in /etc/calamares
	(find calamares -type f -exec install -Dm 644 "{}" "$pkgdir/etc/{}" \;)

	# copy install scripts
	install -Dm 755 post_install.sh					"$_bindir"/post_install.sh
	install -Dm 755 chrooted_post_install.sh		"$_bindir"/chrooted_post_install.sh
	install -Dm 755 post_install_nvidia.sh			"$_bindir"/post_install_nvidia.sh
	install -Dm 755 chrooted_post_install_nvidia.sh	"$_bindir"/chrooted_post_install_nvidia.sh

	# make scripts executable
	chmod 755 "$pkgdir"/etc/calamares/launch.sh
	chmod 755 "$pkgdir"/etc/calamares/branding/archcraft/test-slides.sh
}
